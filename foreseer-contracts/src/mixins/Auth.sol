// SPDX-License-Identifier: MIT
pragma solidity <0.9.0;

import {IAuth} from "../interfaces/IAuth.sol";

/// @title Auth
/// @notice Provides admin and operator roles and access control modifiers
abstract contract Auth is IAuth {
    /// @dev The set of addresses authorized as Admins
    mapping(address => uint256) public admins;

    /// @dev The set of addresses authorized as Operators
    mapping(address => uint256) public operators;

    mapping(address => uint256) public reputationOracle;

    modifier onlyAdmin() {
        if (admins[msg.sender] != 1) revert NotAdmin();
        _;
    }

    modifier onlyOperator() {
        if (operators[msg.sender] != 1) revert NotOperator();
        _;
    }

    modifier onlyReputationOracle() {
        if (reputationOracle[msg.sender] != 1) revert NotReputationOracle();
        _;
    }

    constructor() {
        admins[msg.sender] = 1;
        operators[msg.sender] = 1;
    }

    function isAdmin(address usr) external view returns (bool) {
        return admins[usr] == 1;
    }

    function isOperator(address usr) external view returns (bool) {
        return operators[usr] == 1;
    }

    function isReputationOracle(address usr) external view returns (bool) {
        return reputationOracle[usr] == 1;
    }

    /// @notice Adds a new admin
    /// Can only be called by a current admin
    /// @param admin_ - The new admin
    function addAdmin(address admin_) external onlyAdmin {
        admins[admin_] = 1;
        emit NewAdmin(admin_, msg.sender);
    }

    /// @notice Adds a new operator
    /// Can only be called by a current admin
    /// @param operator_ - The new operator
    function addOperator(address operator_) external onlyAdmin {
        operators[operator_] = 1;
        emit NewOperator(operator_, msg.sender);
    }

    /// @notice Adds a new reputation oracle
    /// Can only be called by a current admin
    /// @param reputationOracle_ - The new reputation oracle
    function addReputationOracle(address reputationOracle_) external onlyAdmin {
        reputationOracle[reputationOracle_] = 1;
        emit NewReputationOracle(reputationOracle_, msg.sender);
    }

    /// @notice Removes an existing Admin
    /// Can only be called by a current admin
    /// @param admin - The admin to be removed
    function removeAdmin(address admin) external onlyAdmin {
        admins[admin] = 0;
        emit RemovedAdmin(admin, msg.sender);
    }

    /// @notice Removes an existing operator
    /// Can only be called by a current admin
    /// @param operator - The operator to be removed
    function removeOperator(address operator) external onlyAdmin {
        operators[operator] = 0;
        emit RemovedOperator(operator, msg.sender);
    }

    /// @notice Removes an existing reputation oracle
    /// Can only be called by a current admin
    /// @param reputationOracle_ - The reputation oracle to be removed
    function removeReputationOracle(address reputationOracle_) external onlyAdmin {
        reputationOracle[reputationOracle_] = 0;
        emit RemovedReputationOracle(reputationOracle_, msg.sender);
    }

    /// @notice Removes the admin role for the caller
    /// Can only be called by an existing admin
    function renounceAdminRole() external onlyAdmin {
        admins[msg.sender] = 0;
        emit RemovedAdmin(msg.sender, msg.sender);
    }

    /// @notice Removes the operator role for the caller
    /// Can only be called by an exiting operator
    function renounceOperatorRole() external onlyOperator {
        operators[msg.sender] = 0;
        emit RemovedOperator(msg.sender, msg.sender);
    }

    /// @notice Removes the reputation oracle role for the caller
    /// Can only be called by an exiting reputation oracle
    function renounceReputationOracleRole() external onlyReputationOracle {
        reputationOracle[msg.sender] = 0;
        emit RemovedReputationOracle(msg.sender, msg.sender);
    }
}
